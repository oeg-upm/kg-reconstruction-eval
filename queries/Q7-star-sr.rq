PREFIX  ex: <http://example.org/>
PREFIX  rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX  rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX  ex: <http://example/>
PREFIX  sem: <http://semmeddb.com/>

CONSTRUCT
  {
  ?predsub_node_iri a rdf:Statement ;
                     rdf:subject ?subject_predication_id ;
                   rdf:object ?subject_name_iri ;
                   rdf:predicate sem:subject ;
                    sem:timestamp ?sub_timestamp .
  ?predobj_node_iri a rdf:Statement ;
                     rdf:subject ?object_predication_id ;
                   rdf:object ?object_name_iri ;
                   rdf:predicate sem:object ;
                    sem:timestamp ?obj_timestamp .

  ?subject_node_iri a rdf:Statement ;
                     rdf:subject ?subject_name_iri ;
                   rdf:object ?subject_semtype ;
                   rdf:predicate sem:semanticType ;
                   sem:score ?subject_score .
  ?object_node_iri a rdf:Statement ;
                     rdf:subject ?object_name_iri ;
               rdf:object ?object_semtype ;
               rdf:predicate sem:semanticType ;
               sem:score ?object_score .

  ?entity_node_iri a rdf:Statement ;
                     rdf:subject ?entity_id_iri ;
               rdf:object ?entity_semtype ;
               rdf:predicate sem:semanticType ;
               sem:score ?entity_score .
  }
WHERE {
  {
  << ?subject_name_iri sem:semanticType ?subject_semtype >> sem:score ?subject_score .
  FILTER(CONTAINS(STR(?subject_name_iri), "http://semmeddb.com/subject/"))
  BIND(URI(CONCAT(STR("http://semmeddb.com/subject-semtype/"),encode_for_uri(str(rand())))) as ?subject_node_iri)
  }
  UNION
  {
  << ?subject_predication_id sem:subject ?subject_name_iri >> sem:timestamp ?sub_timestamp .
  FILTER(CONTAINS(STR(?subject_name_iri), "http://semmeddb.com/subject/"))
  BIND(URI(REPLACE(STR(?subject_predication_id), "http://semmeddb.com/predication/", "http://semmeddb.com/predication-subject/")) as ?predsub_node_iri)
  }
  UNION
  {
  << ?object_name_iri sem:semanticType ?object_semtype >> sem:score ?object_score .
  FILTER(CONTAINS(STR(?object_name_iri), "http://semmeddb.com/object/"))
  BIND(URI(CONCAT(STR("http://semmeddb.com/object-semtype/"),encode_for_uri(str(rand())))) as ?object_node_iri)
  }
  UNION
  {
  << ?object_predication_id sem:object ?object_name_iri >> sem:timestamp ?obj_timestamp .
  FILTER(CONTAINS(STR(?object_name_iri), "http://semmeddb.com/object/"))
  BIND(URI(REPLACE(STR(?object_predication_id), "http://semmeddb.com/predication/", "http://semmeddb.com/predication-object/")) as ?predobj_node_iri)
  }
  UNION
  {
  << ?entity_id_iri sem:semanticType ?entity_semtype >> sem:score ?entity_score .
  FILTER(CONTAINS(STR(?entity_id_iri), "http://semmeddb.com/entity/"))
  BIND(URI(REPLACE(STR(?entity_id_iri), "http://semmeddb.com/entity/", "http://semmeddb.com/entity-semtype/")) as ?entity_node_iri)
  }
}
